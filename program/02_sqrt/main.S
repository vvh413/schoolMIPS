
## unsigned isqrt (unsigned x) {
##     unsigned m, y, b;
##     m = 0x40000000;
##     y = 0;
##     while (m != 0) { // Do 16 times
##         b = y |  m;
##         y >>= 1;
##         if (x >= b) {
##             x -= b;
##             y |= m;
##         }
##         m >>= 2;
##     }
##     return y;
## }

        .text

init:   # rdext   $a0                 # read external value to a0
        # 000000 00000 00000 00100 00000 111110
        # 0000203e
        addu    $0, $0, $0          # replace 00000021 in hex
        li      $v0, 0          ## calculation result reset

sqrt:   lui     $t0, 0x4000     ## m = 0x40000000
        move    $t1, $0         ## y = 0

L0:     or      $t2, $t1, $t0   ## b = y | m;
        srl     $t1, $t1, 1     ## y >>= 1
        sltu    $t3, $a0, $t2   ## if (x < b)
        bnez    $t3, L1         ##   goto L1
                                ## else
        subu    $a0, $a0, $t2   ##   x -= b
        or      $t1, $t1, $t0   ##   y |= m

L1:     srl     $t0, $t0, 2     ## m >>= 2
        bnez    $t0, L0         ## if(m != 0) goto L0
        move    $v0, $t1        ## return y
        # wrext   $v0                 # write v0 to external out
        # 000000 00010 00000 00000 00000 111111
        # 0040003f
        addu    $0, $0, $0          # replace 00000021 in hex

end:    beqz    $0,  end        ## while(1);
